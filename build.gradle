plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.jtool'

java {
    // 依旧保持 java8 保证对 matlab 等应用的兼容性
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven {url 'https://www.jitpack.io'}
}

sourceSets {
    main {java {srcDirs = ['src/main/java']}; resources {srcDirs = ['src/main/resources']}}
}

compileJava {
    options.encoding = "UTF-8"
//  options.compilerArgs << "-Xlint:deprecation"
}

dependencies {
    implementation('org.jetbrains:annotations:24.1.0')              // debug
    
    implementation('org.apache.groovy:groovy:4.0.16')               // 用于直接执行 groovy 脚本而不需要安装 groovy
    implementation('org.apache.groovy:groovy-groovysh:4.0.16')      // 用于直接通过命令行交互试的操作
    implementation('org.apache.groovy:groovy-json:4.0.16')          // 直接使用 groovy 的 json 包来支持 json，可以让 groovy 脚本使用原生的 json 功能
    implementation('org.apache.groovy:groovy-xml:4.0.16')           // groovy 的 xml
    implementation('org.apache.groovy:groovy-yaml:4.0.16')          // groovy 的 yaml，暂时没有用到，并且这个包有些复杂，所以不提供支持
    
    implementation('black.ninia:jep:4.1.1')                         // 调用 python 代码支持，需要系统有需要的 python 环境，并且需要初始化，因此这里不作为主要代码的使用
//  implementation('org.zeromq:jeromq:0.5.4')                       // 跨 JVM 并行库，可以支持超算上跨节点并行；虽然 java 标准库也有类似功能，但是这个更加易用；现在直接使用 mpi
    
//  implementation('org.apache.commons:commons-rng-sampling:1.5')   // 只使用随机数生成器，还是太复杂了
    implementation('me.tongfei:progressbar:0.9.5')                  // 简单的进度条实现（最新版需要 jdk9）
    implementation('org.jfree:jfreechart:1.5.4')                    // 简单的绘图支持
//  implementation('org.apache.commons:commons-pool2:2.11.1')       // 对象池, bad ideal，即使需要也应该自己实现一个简单的实例
    implementation('com.google.collections:google-collections:1.0') // 实现更加简单的容器初始化
//  implementation('org.apache.commons:commons-math3:3.6.1')        // 矩阵计算库，不好用
    implementation('org.apache.commons:commons-csv:1.10.0')         // csv 格式读写，对于复杂的情况还是需要使用库
    implementation('net.jafama:jafama:2.3.2')                       // 牺牲精度快速计算 exp，sin 等
    implementation('com.jcraft:jsch:0.1.55')                        // 用于支持 ssh 连接
    implementation('com.jcraft:jzlib:1.1.3')                        // ssh 压缩传输，并且提供压缩指令支持
//  implementation('com.googlecode.json-simple:json-simple:1.1')    // 小的 json 读写支持，现在直接使用 groovy-json
}

shadowJar {
    // 指定输出的 jar 文件名，默认输出所有的工具包
    archiveBaseName = 'jtool'
    archiveClassifier = 'all'
    
    // 指定主要运行类
    manifest {attributes 'Main-Class': 'jtool.Main'}
    
    // 现在只重命名实际遇到冲突的第三方库，避免意外的问题（主要是 matlab 自带的 jsch）
    relocate('com.jcraft.jsch', 'shade.com.jcraft.jsch')
    relocate('com.jcraft.jzlib', 'shade.com.jcraft.jzlib')
    
    // 修改目标文件夹，打包后添加到子项目中
    destinationDirectory = file('./release/lib')
}

tasks.register('sourceJar', Jar) {
    archiveBaseName = 'jtool'
    archiveClassifier = 'src'
    from(sourceSets.main.allSource)
    exclude('assets/**')
    // 添加过滤器来修复 relocate 的问题
    filteringCharset = 'UTF-8'
    filter {line ->
        line = line.replace('com.jcraft.jsch' , 'shade.com.jcraft.jsch')
        line = line.replace('com.jcraft.jzlib', 'shade.com.jcraft.jzlib')
        return line
    }
    
    // 修改目标文件夹，打包后添加到子项目中
    destinationDirectory = file('./release/src')
}

tasks.register('downloadSource') {
    // 直接硬编码下载第三方库的源码文件
    if (!file('./release/src/groovy-4.0.16-sources.jar').exists()) ant.get(src: 'https://repo1.maven.org/maven2/org/apache/groovy/groovy/4.0.16/groovy-4.0.16-sources.jar', dest: './release/src')
}


build {
    dependsOn shadowJar, sourceJar, downloadSource
    jar.enabled = false
}

