plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.3'
}

group = 'com.jse'

java {
    // 依旧保持 java8 保证对 matlab 等应用的兼容性
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
//  maven {url 'https://www.jitpack.io'}
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
            exclude(
                'assets/jniutil/src/.*/', 'assets/jniutil/src/build/', 'assets/jniutil/src/*build*/',
                'assets/mpi/src/.*/', 'assets/mpi/src/build/', 'assets/mpi/src/*build*/',
                'assets/lmp/src/.*/', 'assets/lmp/src/build/', 'assets/lmp/src/*build*/',
                'assets/jep/jep*-test/'
            )
        }
    }
    jep {
        java {
            srcDirs = ['src/jep/java']
        }
        resources {
            srcDirs = ['src/jep/resources']
        }
    }
    progressbar {
        java {
            srcDirs = ['src/progressbar/java']
        }
    }
}

compileJepJava {options.encoding = 'UTF-8'}
compileProgressbarJava {options.encoding = 'UTF-8'}
compileJava {
    dependsOn(compileJepJava, compileProgressbarJava)
    
    options.fork = true
    options.forkOptions.jvmArgs << '-Duser.language=en' // 直接设置 java 编译器为英文即可避免乱码问题，这应该是最好的方法
    options.encoding = 'UTF-8'
    
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

dependencies {
    implementation('org.jetbrains:annotations:24.1.0')              // idea 调试支持
    
    // 源码依赖
    implementation(sourceSets.jep.output)
    implementation(sourceSets.progressbar.output)
    
    // groovy
    implementation('org.apache.groovy:groovy:4.0.18')               // 用于直接执行 groovy 脚本而不需要安装 groovy
    implementation('org.apache.groovy:groovy-groovysh:4.0.18')      // 用于直接通过命令行交互试的操作
    implementation('org.apache.groovy:groovy-json:4.0.18')          // 直接使用 groovy 的 json 包来支持 json，可以让 groovy 脚本使用原生的 json 功能
    implementation('org.apache.groovy:groovy-xml:4.0.18')           // groovy 的 xml
    implementation('org.apache.groovy:groovy-yaml:4.0.18')          // groovy 的 yaml，暂时没有用到，并且这个包有些复杂，所以不提供支持
    implementation('jline:jline:2.14.6')                            // groovysh 前置，用来重写 sh 相关方法
    
    // 跨语言编程
//  implementation('black.ninia:jep:4.2.0')                         // 调用 python 代码支持，需要系统有需要的 python 环境，并且需要初始化，因此这里不作为主要代码的使用；由于 PyObject 不能 groovy 原生调用，现在包含到项目中
    implementation('com.google.code.gson:gson:2.10.1')              // 使用新版的 gson，避免漏洞警告
    implementation('org.zeromq:jeromq:0.5.4')                       // 跨 JVM 并行库，可以支持超算上跨节点并行；虽然 java 标准库也有类似功能，但是这个更加易用；现在直接使用 mpi
    implementation('io.github.spencerpark:jupyter-jvm-basekernel:2.3.0') {transitive = false} // jupyter 的通用 kernel，关闭传递手动使用新版本的 ZeroMQ 和 Gson
    
    // 实用工具
    implementation('com.mastfrog:sort:2.9.7')                       // 实用的多排序功能，因此现在不再支持支持性不好的 Compare 排序
//  implementation('org.apache.commons:commons-rng-sampling:1.5')   // 只使用随机数生成器，还是太复杂了
//  implementation('me.tongfei:progressbar:0.9.5')                  // 简单的进度条实现（最新版需要 jdk9）；由于字符问题这里直接把源码包含到项目中
    implementation('org.jfree:jfreechart:1.5.4')                    // 简单的绘图支持
//  implementation('org.jfree:jfreesvg:3.4.3')                      // 保存为 SVG 支持，目前没有需求，为了接口一致性这里关掉
//  implementation('org.apache.commons:commons-pool2:2.11.1')       // 对象池, bad ideal，即使需要也应该自己实现一个简单的实例
    implementation('com.google.collections:google-collections:1.0') // 实现更加简单的容器初始化
//  implementation('org.apache.commons:commons-math3:3.6.1')        // 矩阵计算库，不好用
    implementation('org.apache.commons:commons-csv:1.10.0')         // csv 格式读写，对于复杂的情况还是需要使用库
    implementation('net.jafama:jafama:2.3.2')                       // 牺牲精度快速计算 exp，sin 等
    implementation('com.jcraft:jsch:0.1.55')                        // 用于支持 ssh 连接
    implementation('com.jcraft:jzlib:1.1.3')                        // ssh 压缩传输，并且提供压缩指令支持
//  implementation('com.googlecode.json-simple:json-simple:1.1')    // 小的 json 读写支持，现在直接使用 groovy-json
    
    
    // jep 依赖
    jepImplementation('org.jetbrains:annotations:24.1.0')           // idea 调试支持
    jepImplementation('org.apache.groovy:groovy:4.0.18')            // jep 增加 groovy 支持
    
    // progressbar 依赖
    progressbarImplementation('org.jetbrains:annotations:24.1.0')   // idea 调试支持
}

shadowJar {
    // 指定输出的 jar 文件名，默认输出所有的工具包
    archiveBaseName = 'jse'
    archiveClassifier = 'all'
    
    // 指定主要运行类
    manifest {attributes 'Main-Class': 'jse.Main'}
    
    // 现在只重命名实际遇到冲突的第三方库，避免意外的问题（主要是 matlab 自带的 jsch）
    relocate('com.jcraft.jsch', 'shade.com.jcraft.jsch')
    relocate('com.jcraft.jzlib', 'shade.com.jcraft.jzlib')
    
    // 修改目标文件夹，打包后添加到子项目中
    destinationDirectory = mkdir('./release/lib')
}

tasks.register('sourceJar', Jar) {
    archiveBaseName = 'jse'
    archiveClassifier = 'src'
    from(sourceSets.main.allSource, sourceSets.jep.allSource, sourceSets.progressbar.allSource)
    exclude('assets/**')
    // 添加过滤器来修复 relocate 的问题
    filteringCharset = 'UTF-8'
    filter {line ->
        line = line.replace('com.jcraft.jsch' , 'shade.com.jcraft.jsch')
        line = line.replace('com.jcraft.jzlib', 'shade.com.jcraft.jzlib')
        return line
    }
    
    // 修改目标文件夹，打包后添加到子项目中
    destinationDirectory = mkdir('./release/src')
}

tasks.register('downloadSource') {
    mkdir('./release/src')
    // 直接硬编码下载第三方库的源码文件
    if (!file('./release/src/groovy-4.0.18-sources.jar').exists()) ant.get(src: 'https://repo1.maven.org/maven2/org/apache/groovy/groovy/4.0.18/groovy-4.0.18-sources.jar', dest: './release/src')
}


build {
    dependsOn(shadowJar, sourceJar, downloadSource)
    jar.enabled = false
}

