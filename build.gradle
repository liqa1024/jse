plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

java {
    // 暂定版本为 jdk 1.8，使用过高的版本会因为弃用的函数而报错
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

sourceSets {
    main {java {srcDirs = ['src/main/java']}; resources {srcDirs = ['src/main/resources']}}
}

compileJava {
    options.encoding = "UTF-8"
}

dependencies {
    compileOnly('org.jetbrains:annotations:24.0.1')                 // debug
    
    implementation('org.apache.groovy:groovy:4.0.11')               // 用于直接执行 groovy 脚本而不需要安装 groovy
    implementation('org.apache.groovy:groovy-json:4.0.11')          // 直接使用 groovy 的 json 包来支持 json，可以让 groovy 脚本使用原生的 json 功能
//  implementation('org.apache.groovy:groovy-yaml:4.0.11')          // groovy 的 yaml，暂时没有用到，并且这个包有些复杂，所以不提供支持
    
    implementation('black.ninia:jep:4.1.1')                         // 调用 python 代码支持，需要系统有需要的 python 环境，并且需要初始化，因此这里不作为主要代码的使用
    
    implementation('org.jfree:jfreechart:1.5.4')                    // 简单的绘图支持
    
//  implementation('org.apache.commons:commons-pool2:2.11.1')       // 对象池, bad ideal，即使需要也应该自己实现一个简单的实例
    implementation('com.google.collections:google-collections:1.0') // 实现更加简单的容器初始化
//  implementation('org.apache.commons:commons-math3:3.6.1')        // 矩阵计算库，不好用
//  implementation('org.apache.commons:commons-csv:1.10.0')         // csv 格式读写，现在改为自己实现
    implementation('net.jafama:jafama:2.3.2')                       // 牺牲精度快速计算 exp，sin 等
    implementation('com.jcraft:jsch:0.1.55')                        // 用于支持 ssh 连接
    implementation('com.jcraft:jzlib:1.1.3')                        // ssh 压缩传输，并且提供压缩指令支持
//  implementation('com.googlecode.json-simple:json-simple:1.1')    // 小的 json 读写支持，现在直接使用 groovy-json
}

shadowJar {
    // 指定输出的 jar 文件名，默认输出所有的工具包
    archiveFileName.set('jTool-all.jar')
    
    // 指定主要运行类
    manifest {attributes 'Main-Class': 'com.jtool.Main'}
    
    // 重命名使用的第三方库，避免命名冲突（不鼓励直接使用，因为可能会变动）
    relocate('org.apache.commons', 'shade.org.apache.commons')
    relocate('assets.org.apache.commons', 'assets.shade.org.apache.commons')
    relocate('com.google.common', 'shade.com.google.common')
    relocate('net.jafama', 'shade.net.jafama')
    relocate('com.jcraft.jsch', 'shade.com.jcraft.jsch')
    relocate('com.jcraft.jzlib', 'shade.com.jcraft.jzlib')
    relocate('org.jfree', 'shade.org.jfree')
    relocate('com.fasterxml.jackson', 'shade.com.fasterxml.jackson')
    relocate('org.yaml.snakeyaml', 'shade.org.yaml.snakeyaml')
//  relocate('org.json.simple', 'shade.org.json.simple')
    // 不重命名 groovy 相关名称，避免 asm 错误，也方便 groovy 脚本直接使用 groovy 相关部分
    
    // 修改目标文件夹，打包后添加到子项目中
    destinationDirectory = file('./release/lib')
}

build {
    dependsOn shadowJar
    jar.enabled = false
}
